#!/usr/bin/env perl
use strict;
use warnings;

use GraphViz2;
use Music::BachChoralHarmony;

my $id = shift || '000106b_';

my $bach = Music::BachChoralHarmony->new;

my $progression = $bach->parse($id);

my %bigram;
my %score;

my $last = '';

for my $struct ( @$progression ) {
    my $chord = $struct->{chord};

    if ( $last ) {
        $score{ $last . ' ' . $chord }++;

        if ( !grep { $last eq $_ } @{ $bigram{$chord} } ) {
            push @{ $bigram{$last} }, $chord;
        }
    }

    $last = $chord;
}

my $g = GraphViz2->new(
    global => { directed => 1 },
    node   => { shape => 'oval' },
    edge   => { color => 'grey' },
);

my %nodes;
my %edges;

for my $i ( keys %bigram ) {
    $g->add_node( name => $i )
        unless $nodes{$i}++;

    for my $j ( @{ $bigram{$i} } ) {
        $g->add_node( name => $j )
            unless $nodes{$j}++;

        my $name = $i . ' ' . $j;
        $g->add_edge( from => $i, to => $j, label => $score{$name} )
            unless $edges{$name}++;
    }
}

$g->run( format => 'png', output_file => $0 . '.png' );
